buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        //noinspection GradleDynamicVersion
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
defaultTasks 'clean', 'build'

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {

    mavenLocal()
    mavenCentral()
    maven {
        url "${artifactory_contextUrl}/mobile-releases"
    }
    maven { url 'https://maven.fabric.io/public' }

}

android {
    compileSdkVersion 21
    buildToolsVersion '21.1.2'
    compileOptions.encoding = "UTF-8"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 21
        versionName = "3.3.0" + "-" + System.getenv()['BUILD_NUMBER']
        versionCode = 52
    }

    productFlavors {

        dev {
            applicationId "com.blinkboxbooks.android.dev"
        }

        qa {
            applicationId "com.blinkboxbooks.android.qa"
        }

        production {
            applicationId "com.blinkboxbooks.android"
        }
    }

    lintOptions {
        abortOnError true
    }

    buildTypes {

        release {

            minifyEnabled true
            proguardFile getDefaultProguardFile('proguard-android.txt')
            proguardFile 'proguard-project.txt'
        }

        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                def outputFile = output.outputFile
                if (outputFile != null && outputFile.name.endsWith('.apk')) {
                    def suffix = variant.buildType.name == "release" ? "-${variant.productFlavors[0].name}" : "-${variant.productFlavors[0].name}-${variant.buildType.name}"
                    output.outputFile = file("${project.buildDir}/apk/bbbooks-${defaultConfig.versionName}${suffix}.apk")
                }
            }
        }
    }
 }

dependencies {
    compile 'com.blinkbox.books:volley:1.1.20'
    compile 'com.blinkbox.books:bbb-android-sdk:1.6.54'
    compile 'com.blinkbox.books:bbb-epub-library:1.3.0'
    compile 'com.google.code.gson:gson:2.3'
    compile 'com.android.support:support-v4:21.0.3'
    compile 'com.android.support:appcompat-v7:21.0.3'
    compile 'com.google.android.gms:play-services:6.5.87'
    compile 'it.sephiroth.android.library.imagezoom:imagezoom:2.1.1'
    compile 'com.android.support:recyclerview-v7:21.0.3'

    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile('com.crashlytics.sdk.android:crashlytics:2.2.0@aar') {
        transitive = true;
    }
}

task monkeysh(type: Exec) {
    commandLine 'scripts/monkey_run.sh'
}